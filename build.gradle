// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.4.32'
    repositories {
        google()
        //noinspection JcenterRepositoryObsolete
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.2.0-rc01'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlin_version"
        classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:4.20.0'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    apply plugin: "com.jfrog.artifactory"
    apply plugin: 'maven-publish'

    repositories {
        //noinspection JcenterRepositoryObsolete
//        jcenter()
        google()
        maven { url "https://codecodecoffee.jfrog.io/ui/#/builds/gatekeeper/1618744122597" }

        mavenCentral()
    }
}
artifactoryPublish.skip = true

project('gatekeeper') {
    artifactoryPublish.dependsOn("assemble")

    publishing {
        publications {
            aar(MavenPublication) {
                artifactId = "gatekeeper"
                groupId = "com.codecodecoffee"
                version = '1.0.0-alpha4'
                artifact("$buildDir/outputs/aar/gatekeeper-release.aar")
            }
        }
    }

    artifactoryPublish {
        publications(publishing.publications.aar)
    }
}


artifactory {
    contextUrl = "${artifactory_contextUrl}"
    //The base Artifactory URL if not overridden by the publisher/resolver
    publish {
        repository {
            repoKey = 'codecodecoffee-gradle-dev-local'
            username = "${artifactory_user}"
            password = "${artifactory_password}"
            maven = true
        }
        defaults {
            publishArtifacts = true
            publishPom = true
        }
    }
    resolve {
        repository {
            repoKey = 'codecodecoffee-gradle-dev'
            username = "${artifactory_user}"
            password = "${artifactory_password}"
            maven = true

        }
    }
//    resolve {
//        repository {
//            repoKey = 'gatekeeper-gradle-dev'
//            username = "${artifactory_user}"
//            password = "${artifactory_password}"
//            maven = true
//
//        }
//    }

}


task clean(type: Delete) {
    delete rootProject.buildDir
}
